OROCAPI ; JMH - ORDER CHECK APIs;8/24/07 ;3/19/08  12:17
 ;;3.0;ORDER ENTRY/RESULTS REPORTING;**272**;Dec 17, 1997;Build 53
 ;;
DOC(DFN,OI) ;DANGEREOUS MEDS FOR PAT > 64 ORDER CHECK
 ;DFN=FILE2 IEN
 ;OI=FILE 101.43 IEN
 N RET,VAL,I,IN,MODE
 S MODE="ACCEPT"
 S IN=OI_"|PS|^^99NDF^^^99PSD"
 D EN^OCXOEPS(.RET,DFN,IN,MODE)
 S I=0 F  S I=$O(RET(I)) Q:'I  D
 .I $P(RET(I),U,2)=30 S VAL=RET(I)
 Q $G(VAL)
GOC(DFN,MEDTXT) ;GLUCOPHAGE LAB RESULTS ORDER CHECK
 ;DFN=FILE 2 IEN
 ;MEDTXT=FILE 50 LOCAL DRUG TEXT
 N RET,IN,MODE,VAL,I
 S MODE="SELECT"
 S IN="|PS|^^99NDF^^"_MEDTXT_"^99PSD"
 D EN^OCXOEPS(.RET,DFN,IN,MODE)
 S I=0 F  S I=$O(RET(I)) Q:'I  D
 .I $P(RET(I),U,2)=28 S VAL=RET(I)
 Q $G(VAL)
AOC(DFN,ORPROD) ;AMINOGLYCOSIDE ORDERED ORDER CHECK
 ;DFN=FILE 2 IEN
 ;ORPROD=FILE 50.68 IEN (VA PRODUCT)
 N VAL,RET,I,ORPS,IN,MODE
 S MODE="SESSION"
 S IN="|PS|."_ORPROD_"^^99NDF^^^99PSD"
 D EN^OCXOEPS(.RET,DFN,IN,MODE)
 S I=0 F  S I=$O(RET(I)) Q:'I  D
 .I $P(RET(I),U,2)=20 S VAL=RET(I)
 Q $G(VAL)
 ;
