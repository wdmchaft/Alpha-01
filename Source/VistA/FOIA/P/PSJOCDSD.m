PSJOCDSD ;BIR/MV - PROCESS DOSING ORDER CHECKS ;6 Jun 07 / 3:37 PM
 ;;5.0; INPATIENT MEDICATIONS ;**181**;16 DEC 97;Build 190
 ;
DISPLAY ;Display dose checks
 NEW PSJPON,PSJDSPFG,PSJCNT0
 D FULL^VALM1
 Q:'$$DSPSERR^PSJOC("No dosing checks can be performed")
 D EXCEPTN2
 K PSJDSPFG
 F PSJCNT0=0:0 S PSJCNT0=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0)) Q:'PSJCNT0  Q:$G(PSGORQF)  D
 . S PSJPON="" F  S PSJPON=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON)) Q:PSJPON=""  Q:$G(PSGORQF)  D
 .. Q:PSJPON=0
 .. D ERROR
 .. D EXCEPTN
 .. D WARNING
 .. I +$G(PSJDSPFG) D PAUSE^PSJLMUT1 W @IOF
 K PSJDSPFG
 Q
WARNING ;Display warning messages
 NEW PSJSGLE,PSJRNGE,PSJMSG,PSJDD,PSJTYPE
 S PSJMSG=""
 S (PSJSGLE,PSJRNGE)=0
 I '$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",0)),'$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",0)) W !
 S PSJTYPE="" F  S PSJTYPE=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE)) Q:PSJTYPE=""  D
 . W !
 . F PSJDD=0:0 S PSJDD=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE,PSJDD)) Q:'PSJDD  D
 .. Q:$G(PSGORQF)
 .. S PSJMSG=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"MESSAGE",PSJTYPE,PSJDD))
 .. I ($Y+6)>IOSL D PAUSE^PSJLMUT1 W @IOF
 .. D WRITE^PSJMISC(PSJMSG,3)
 .. S:PSJTYPE["1_SINGLE" PSJSGLE=1_U_PSJDD
 .. S:PSJTYPE["2_RANGE" PSJRNGE=1_U_PSJDD
 .. S:PSJTYPE["3_GENERAL" PSJDSPFG=1
 Q:$G(PSGORQF)
 D INTERV
 Q
INTERV ;Process intervention for dosing check
 NEW PSJDD
 S PSJDD=$S(+PSJSGLE:$P(PSJSGLE,U,2),1:$P(PSJRNGE,U,2))
 I 'PSJDD S PSJDSPFG=1 Q
 K PSJDSPFG
 W !
 I +PSJSGLE,+PSJRNGE D RINTERV^PSJGMRA("MAX SINGLE DOSE & DAILY DOSE RANGE") Q
 I +PSJRNGE D RINTERV^PSJGMRA("DAILY DOSE RANGE") Q
 I +PSJSGLE D RINTERV^PSJGMRA("MAX SINGLE DOSE") Q
 Q
ERROR ; Process errors
 NEW PSJCNT,PSJNV
 ;PSJDSPFG - Display pause if there were errors
 ;Check for system error one more time.
 S PSJDSPFG=0
 I $O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",0)) W !!
 F PSJCNT=0:0 S PSJCNT=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT)) Q:'PSJCNT  D
 . S PSJDSPFG=1
 . W !
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT,"MSG"))
 . D:PSJNV]"" WRITE^PSJMISC(PSJNV,1)
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"ERROR",PSJCNT,"TEXT"))
 . D:PSJNV]"" WRITE^PSJMISC(PSJNV,3)
 Q
EXCEPTN ; Process exceptions
 NEW PSJCNT,PSJNV,PSJSPACE,PSJQFLG1
 ;PSJDSPFG - Display pause if there were errors
 ;Check for system error one more time.
 S PSJDSPFG=0,PSJSPACE=0
 I $O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",0)) W !!
 F PSJCNT=0:0 S PSJCNT=$O(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT)) Q:'PSJCNT  D
 . S PSJQFLG1=0
 . I $G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Dosing Checks could not be performed",($G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT+1))["Drug not matched to NDF") Q
 . I $G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Drug not matched to NDF" Q
 . I $G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))["Order Checks could not be done" Q
 . S PSJDSPFG=1
 . S PSJNV=$G(^TMP($J,"PSJPRE1","OUT",PSJCNT0,PSJPON,"EXCEPTIONS",PSJCNT))
 . I PSJNV]"" D
 .. I ($Y+6)>IOSL D PAUSE^PSJLMUT1 W @IOF
 .. ;If the output has 13 leading spaces, strip it off so the display is indenting correctly
 .. I $E(PSJNV,1,13)="             " S PSJSPACE=13,PSJNV=$P(PSJNV,"             ",2)
 .. D WRITE^PSJMISC(PSJNV,PSJSPACE+3)
 Q
EXCEPTN2 ; Process exceptions on prospective drug
 NEW PSJPON,PSJN,PSJNV
 K PSJDSPFG
 S PSJPON="" F  S PSJPON=$O(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON)) Q:PSJPON=""  D
 . F PSJN=0:0 S PSJN=$O(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON,PSJN)) Q:'PSJN  D
 .. S PSJNV=$G(^TMP($J,"PSJPRE","OUT","EXCEPTIONS",PSJPON,PSJN))
 .. I $P(PSJPON,";",3)="PROFILE" Q
 .. I '$$ERRCHK^PSJOC("PROSPECTIVE",$P(PSJNV,U,3)_$P(PSJNV,U,10)) Q
 .. W !
 .. D DSPDRGER^PSJOC(1)
 I $G(PSJDSPFG) D PAUSE^PSJLMUT1 W @IOF
 Q
